require "atmos.env.pico"

set X     = require "atmos.x"
val pico  = require "pico"
val art   = require "art"
val Panel = require "panel"

set W = 120
set H = 120
set R = 6
set w = W/R
set h = H/R

pico.zet.title("Fábrica");
pico.zet.size.window(@{x=R*W,y=R*H}, @{x=W,y=H})
pico.zet.expert(true);

func pos_lc (pos) {
    @{
        l = pos.y // R,
        c = pos.x // R,
    }
}

func lc_pos (lc) {
    @{
        x = lc.c * R,
        y = lc.l * R,
    }
}

set MAP = @{}
loop l in h {
    set MAP[l] = @{}
    loop c in w {
        set MAP[l][c] = nil     ;; { img=buffer, rot=degrees }
    }
}

math.randomseed()

spawn {
    every :draw {
        loop l in h {
            loop c in w {
                val t = MAP[l][c]
                if t {
                    val pos = @{ l=l-1, c=c-1 } -> lc_pos
                    pico.zet.anchor.draw(:left, :top)
                    pico.zet.rotate(t.rot)
                    pico.output.draw.buffer(pos, t.img)
                    pico.zet.rotate(0)
                }
            }
        }
    }
}

spawn Panel()

pin hand = tasks(1)     ;; somente 1 peça "na mão" por vez
set HAND = hand         ;; torna mão uma global

func Piece (this) {
    watching :mouse.button.dn, :right {
        spawn {
            every :draw {
                val pos = pico.get.mouse() -> pos_lc -> lc_pos
                pico.zet.anchor.draw(:top, :left)
                pico.zet.rotate(this.rot)
                pico.output.draw.buffer(pos, this.img)
                pico.zet.rotate(0)
            }
        }
        do :ok {
            loop {
                await(:mouse.button.dn, :left)
                watching @.200 {
                    await(:mouse.button.dn, :left)
                    escape :ok
                }
                set this.rot = this.rot + 90
            }
        }
        val lc = pico.get.mouse() -> pos_lc
        set MAP[lc.l+1][lc.c+1] = this
    }
}

await(false)
