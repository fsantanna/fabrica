require "atmos.env.pico"

set X     = require "atmos.x"
val pico  = require "pico"
val Panel = require "panel"
val Start = require "start"

set R = 6           ;; tile dimensions in pixels
set w = 4*4         ;; map max number of tiles in X
set h = 4*4         ;; map max number of tiles in Y
val W = R*w         ;; window has w/h tiles
val H = R*(h+3)     ;; (+ panel region at the bottom)

pico.zet.title("FÃ¡brica");
pico.zet.dim.window(5*W, 5*H)   ;; 5 = pixel size
pico.zet.dim.world(W, H)
pico.zet.expert(true);

func Clicks (but, f) {
    val m = await(:mouse.button.dn, but, f)
    watching @.200 {
        await(:mouse.button.dn, but, \{ it.pos === m.pos })
        return(2)
    }
    1
}

func Double_Click (but, f) {
    loop {
        val m = await(:mouse.button.dn, but, f)
        watching @.200 {
            await(:mouse.button.dn, but, \{ it.pos === m.pos })
            return()
        }
    }
}

func pos_lc (pos) {
    @{
        l = pos.y // R,
        c = pos.x // R,
    }
}

func lc_pos (lc) {
    @{
        x = lc.c * R,
        y = lc.l * R,
    }
}

;;;
Tile = @{
    tag = (:machine | :pipe.line | :pipe.curve),
    l   = l,        ;; line
    c   = c,        ;; column
    img = art.imgs.*,    ;; art buffer
    rot = degrees,  ;; rotation
}
;;;

set MAP = @{}
loop l in h {
    set MAP[l] = @{}
    loop c in w {
        set MAP[l][c] = nil
    }
}

set PLAYERS = @{
    active = nil,
    :blue, :brown,
    ;; :white, :gray    ;; TODO: other colors?
}

math.randomseed()

spawn {
    every :draw {
        pico.zet.anchor.pos(:left, :top)
        loop l in 4 {
            loop c in 4 {
                pico.zet.style(:stroke)
                pico.zet.color.draw(pico.color.gray)
                val r = @{ x=(c-1)*4*R, y=(l-1)*4*R, w=4*R, h=4*R }
                pico.output.draw.rect(r)
            }
        }
        loop l in h {
            loop c in w {
                val t = MAP[l][c]
                if t {
                    val pos = @{ l=l-1, c=c-1 } -> lc_pos
                    pico.zet.rotate(t.rot)
                    pico.output.draw.buffer(pos, t.img)
                    pico.zet.rotate(0)
                }
            }
        }
        pico.zet.anchor.pos(:center, :middle)
    }
}

val r = @{ x=W/2, y=H-(1.5*R), w=W, h=3*R }
spawn Panel(r)

pin hand = tasks(1)     ;; only 1 tile at hand
set HAND = hand         ;; make HAND a global

loop i, c {
    set PLAYERS.active = c
    await Start()
}
set PLAYERS.active = PLAYERS[1]

await(false)
