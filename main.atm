require "atmos.env.pico"

set X     = require "atmos.x"
val pico  = require "pico"
val art   = require "art"
val Panel = require "panel"

set R = 6
set w = 15
set h = 15
set W = R*w
set H = R*(h+2)

pico.zet.title("Fábrica");
pico.zet.dim.window(R*W, R*H)
pico.zet.dim.world(W, H)
pico.zet.expert(true);

func pos_lc (pos) {
    @{
        l = pos.y // R,
        c = pos.x // R,
    }
}

func lc_pos (lc) {
    @{
        x = lc.c * R,
        y = lc.l * R,
    }
}

set MAP = @{}
loop l in h {
    set MAP[l] = @{}
    loop c in w {
        set MAP[l][c] = nil     ;; { img=buffer, rot=degrees }
    }
}

math.randomseed()

spawn {
    every :draw {
        loop l in h {
            loop c in w {
                val t = MAP[l][c]
                if t {
                    val pos = @{ l=l-1, c=c-1 } -> lc_pos
                    pico.zet.anchor.pos(:left, :top)
                    pico.zet.rotate(t.rot)
                    pico.output.draw.buffer(pos, t.img)
                    pico.zet.rotate(0)
                    pico.zet.anchor.pos(:center, :middle)
                }
            }
        }
    }
}

val r = @{ x=W/2, y=H-R, w=W, h=2*R }
X.print(r)
spawn Panel(r)

pin hand = tasks(1)     ;; somente 1 peça "na mão" por vez
set HAND = hand         ;; torna mão uma global

func Piece (this) {
    watching :mouse.button.dn, :right {
        spawn {
            every :draw {
                val pos = pico.get.mouse() -> pos_lc -> lc_pos
                pico.zet.anchor.pos(:top, :left)
                pico.zet.rotate(this.rot)
                pico.output.draw.buffer(pos, this.img)
                pico.zet.rotate(0)
                pico.zet.anchor.pos(:center, :middle)
            }
        }
        loop {
            await(:mouse.button.dn, :left)
            val e = watching @.200 {
                await(:mouse.button.dn, :left)
                val lc = pico.get.mouse() -> pos_lc
                if (lc.c < w) && (lc.l < h) {
                    set MAP[lc.l+1][lc.c+1] = this
                    return()  ;; piece placed
                }
            }
            if e == :clock {
                set this.rot = this.rot + 90
            }
        }
    }
    ;; piece displaced
}

await(false)
